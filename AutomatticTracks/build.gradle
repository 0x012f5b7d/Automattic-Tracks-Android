buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.2'
        classpath 'com.novoda:bintray-release:0.3.4'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

repositories {
    mavenCentral()
}

dependencies {
}

android {
    compileSdkVersion 24
    buildToolsVersion "23.0.3"

    defaultConfig {
        versionName "1.1.1"
        minSdkVersion 14
        targetSdkVersion 24
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

version android.defaultConfig.versionName
group = "com.automattic"
archivesBaseName = "tracks"

signing {
    required {
        has("release") && project.properties.containsKey("signing.keyId") && project.properties.containsKey("signing.secretKeyRingFile")
    }
    sign configurations.archives
}

// http://central.sonatype.org/pages/gradle.html

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: project.properties.ossrhUsername, password: project.properties.ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: project.properties.ossrhUsername, password: project.properties.ossrhPassword)
            }

            pom.project {
                name 'Automattic-Tracks-Android'
                packaging 'aar'
                description 'Android client for Nosara tracks (event tracking and analytics)'
                url 'https://github.com/Automattic/Automattic-Tracks-Android'
                scm {
                     connection 'scm:git:https://github.com/Automattic/Automattic-Tracks-Android.git'
                     developerConnection 'scm:git:https://github.com/Automattic/Automattic-Tracks-Android.git'
                     url 'https://github.com/Automattic/Automattic-Tracks-Android'
                 }
                 licenses {
                     license {
                         name 'The GPLv2 License'
                         url 'https://www.gnu.org/licenses/old-licenses/gpl-2.0.html'
                     }
                 }
                developers {
                    developer {
                        id 'daniloercoli'
                        name 'Danilo Ercoli'
                        email 'danilo@automattic.com'
                    }
                }
            }
        }
    }
}
